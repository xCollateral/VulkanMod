#version 460

layout(location = 0) in vec3 Position;
layout(location = 1) in vec4 Color;
layout(location = 2) in vec3 Normal;

layout(binding = 0) uniform readonly UniformBufferObject {
    mat4 ProjMat[32];
};

//Exploit aliasing and allow new Uniforms to overwrite the prior content: reducing required PushConstant Range
layout(push_constant) readonly uniform  PushConstant
{
    vec2 ScreenSize;
    float LineWidth;
};

layout(location = 0) out vec4 vertexColor;
layout(location = 1) out float vertexDistance;

const float VIEW_SHRINK = 1.0 - (1.0 / 256.0);
const mat4 VIEW_SCALE = mat4(
    VIEW_SHRINK, 0.0, 0.0, 0.0,
    0.0, VIEW_SHRINK, 0.0, 0.0,
    0.0, 0.0, VIEW_SHRINK, 0.0,
    0.0, 0.0, 0.0, 1.0
);

void main() {
    vec4 linePosStart = ProjMat[gl_BaseInstance + 1 & 31] * VIEW_SCALE * ProjMat[gl_BaseInstance & 31] * vec4(Position, 1.0);
    vec4 linePosEnd = ProjMat[gl_BaseInstance + 1 & 31] * VIEW_SCALE * ProjMat[gl_BaseInstance & 31] * vec4(Position + Normal, 1.0);

    vec3 ndc1 = linePosStart.xyz / linePosStart.w;
    vec3 ndc2 = linePosEnd.xyz / linePosEnd.w;

    vec2 lineScreenDirection = normalize((ndc2.xy - ndc1.xy) * ScreenSize);
    vec2 lineOffset = vec2(-lineScreenDirection.y, lineScreenDirection.x) * LineWidth / ScreenSize;

    if (lineOffset.x < 0.0) {
        lineOffset *= -1.0;
    }

    if (mod(gl_VertexIndex, 2) == 0) {
        gl_Position = vec4((ndc1 + vec3(lineOffset, 0.0)) * linePosStart.w, linePosStart.w);
    } else {
        gl_Position = vec4((ndc1 - vec3(lineOffset, 0.0)) * linePosStart.w, linePosStart.w);
    }

    vertexDistance = length((vec4(Position, 1.0)).xyz);
    vertexColor = Color;
}

